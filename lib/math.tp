import lang;
import collections;

require {
    clock, nat_cos, nat_log, exit
}

const E: float = 2.7182818284590452354;
const PI: float = 3.14159265358979323846;
const DEG_TO_RAD: float = 0.017453292519943295;
const RAD_TO_DEG: float = 57.29577951308232;

var expN: int = sizeof(int) * 32;
var powN: int = 21;
var cosN: int = sizeof(float) * 4;

const NaN: float = 0.0 / 0.0;
const LN2: float = 0.6931471805599453094;

// basic

fn abs(x: int) int {
    return if x < 0 then -x else x;
}

fn abs(x: float) float {
    return if x < 0 then -x else x;
}

fn isNaN(x: float) int {
    return x != x;
}

fn max(a: int, b: int) int {
    return if a < b then b else a;
}

fn max(a: float, b: float) float {
    return if a < b then b else a;
}

fn min(a: int, b: int) int {
    return if a < b then a else b;
}

fn min(a: float, b: float) float {
    return if a < b then a else b;
}

// exp

fn logE(x: float) float {
    return nat_log(x);
}

fn log2(x: float) float {
    return log(2, x);
}

fn log(base: float, x: float) float {
    return logE(x) / logE(base);
}

fn log(base: int, x: float) float {
    return logE(x) / logE(base as float);
}

fn pow(base: int, expo: int) int {
    if expo == 0 {
        return 1;
    }
    posExp := abs(expo);
    res := 1;
    for i := 0; i < posExp; i++ {
        res *= base;
    }
    return if expo > 0 then res else 1 / res;
}

fn pow(base: float, expo: float) float {
    if expo == 0 {
        return 1.0;
    }
    posExp := abs(expo);
    powOf2 := posExp * log2(base);
    // taylor expansion
    res := 1.0;
    x := powOf2;
    expLn := LN2;
    factN := 1;
    for i := 1; i < powN; i++ {
        factN *= i;
        res += expLn * x / factN;
        x *= powOf2;
        expLn *= LN2;
    }
    return if expo > 0 then res else 1.0 / res;
}

// algebraic

/*
 * Returns the factorial of <n>.
 */
fn fact(n: int) int {
    if n < 0 {
        println("Negative factorial.");
        exit(1);
    }
    x := 1;
    for i := 1; i <= n; ++i {
        x *= i;
    }
    return x;
}

fn fib(n: int) int {
    if n < 2 {
        return n;
    } else {
        return fib(n - 2) + fib(n - 1);
    }
}

fn gcd(x: int, y: int) int {
    absX := abs(x);
    absY := abs(y);
    if absX == absY {
        return absX;
    }
    a := max(absX, absY);
    b := min(absX, absY);
    if b == 0 {
        return a;
    }
    r: int;
    while (r = a % b) != 0 {
        a = b;
        b = r;
    }
    return b;
}

/*
 * Returns a list of primes that less than or equal to <limit>.
 */
fn primes(limit: int) Integer[] {
    lst := new List<Integer>();
    for i := 2; i <= limit; i++ {
        lst.append(new Integer(i));
    }
    index := 0;
    tar: int = lst.get(0).intValue();
    while lst.get(lst.size() - 1).intValue() > tar * tar {
        tar = lst.get(index).intValue();
        //lst = functions.filter(lst, x -> x == tar || x % tar != 0);
        newList := new List<Integer>();
        for item: *Integer in lst {
            if item.intValue() == tar or item.intValue() % tar != 0 {
                newList.append(item);
            }
        }
        del lst;
        lst = newList;
        index += 1;
    }
    array := lst.toArray();
    del lst;
    return array;
}

// random

randSeed: int = 0;

fn random() float {
    const m := (1 << 15) - 1;
    const a := 3;
    seed := clock() + randSeed;
    for i := 0; i < 100; i++ {
        seed = seed * a % m;
    }
    randSeed = seed;
    return (seed as float) / 32768;
}

fn randInt(low: int, high: int) int {
    range1 := high - low;
    r := random() * range1;
    return (r as int) + low;
}

// trigonometry

fn cos(x: float) float {
    ind := 1;
    xSqr := x * x;
    up := xSqr;
    low := 2;
    bound := cosN * 2;
    res := 1.0;
    // cos x = sum (-1)^n * x^(2n) / (2n)!
    for i := 0; i < bound; i += 2 {
        ind = -ind;
        res += ind * up / low;
        up *= xSqr;
        low *= (i + 3) * (i + 4);
    }
    return res;
}

fn cos(x: int) float {
    return cos(x as float);
}

fn sin(x: float) float {
    return cos(PI / 2 - x);
}

fn sin(x: int) float {
    return sin(x as float);
}

fn tan(x: float) float {
    return sin(x) / cos(x);
}

fn tan(x: int) float {
    return tan(x as float);
}

export {
    E, PI, NaN,
    abs, max, min, isNaN,
    fib, gcd, primes,
    random, randInt,
    cos, sin, tan,
}
